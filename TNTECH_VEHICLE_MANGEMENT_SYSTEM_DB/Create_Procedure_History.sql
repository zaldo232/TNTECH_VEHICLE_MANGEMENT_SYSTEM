/**
 * @file        Create_Procedure_History.sql
 * @description 차량 운행 이력 및 배차 상태 목록 조회 프로시저 정의
 */

USE TNTECH_VEHICLE_MANGEMENT_SYSTEM
GO

/**
 * [차량 운행 이력 조회]
 * @param @MEMBER_ID     - 특정 사원 아이디 (NULL일 경우 전체 조회)
 * @param @FILTER_TYPE   - 상태 필터 (ALL, RESERVED, COMPLETED, RETURNED, CANCELED)
 * @param @LICENSE_PLATE - 검색 대상 차량 번호
 * @param @MONTH         - 조회 대상 월 (YYYY-MM)
 * 설명: 사원, 차량, 기간 등 검색 조건에 따른 전체 운행 및 배차 기록 반환
 */
CREATE OR ALTER PROCEDURE SP_GET_HISTORY_LIST
    @MEMBER_ID    NVARCHAR(50) = NULL,
    @FILTER_TYPE  NVARCHAR(20),
    @LICENSE_PLATE NVARCHAR(20) = NULL,
    @MONTH        NVARCHAR(7) = NULL 
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        D.DISPATCH_ID, D.MEMBER_ID, M.MEMBER_NAME, M.DEPARTMENT,
        D.LICENSE_PLATE, V.VEHICLE_NAME, 
        D.RENTAL_DATE, D.RETURN_DATE,
        D.DISPATCH_STATUS, D.REGION, D.VISIT_PLACE, D.BUSINESS_TYPE,
        D.START_MILEAGE, D.END_MILEAGE,
        ISNULL(D.END_MILEAGE, 0) - ISNULL(D.START_MILEAGE, 0) - ISNULL(D.COMMUTE_DISTANCE, 0) AS BUSINESS_DISTANCE 
    FROM TB_DISPATCH D
    LEFT JOIN TB_MEMBERS M ON D.MEMBER_ID = M.MEMBER_ID
    LEFT JOIN TB_VEHICLES V ON D.LICENSE_PLATE = V.LICENSE_PLATE
    WHERE 
        (@MEMBER_ID IS NULL OR D.MEMBER_ID = @MEMBER_ID)
        AND (
            @FILTER_TYPE = 'ALL' 
            OR (@FILTER_TYPE = 'RESERVED' AND D.DISPATCH_STATUS = 'RESERVED')
            OR (@FILTER_TYPE = 'COMPLETED' AND D.DISPATCH_STATUS = 'COMPLETED')
            OR (@FILTER_TYPE = 'RETURNED' AND D.DISPATCH_STATUS = 'RETURNED')
            OR (@FILTER_TYPE = 'CANCELED' AND D.DISPATCH_STATUS = 'CANCELED') 
        )
        AND (@LICENSE_PLATE IS NULL OR TRIM(D.LICENSE_PLATE) = TRIM(@LICENSE_PLATE))
        AND (@MONTH IS NULL OR FORMAT(D.RENTAL_DATE, 'yyyy-MM') = @MONTH)
    ORDER BY D.RENTAL_DATE DESC, D.DISPATCH_ID DESC; 
END
GO